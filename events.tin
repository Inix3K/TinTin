#class {events} {open}
#NOP REQUIRED:  draw.tin	//split variable

/* Screen Sizing */
#event {SCREEN RESIZE} {
        #read .tt/${mud}/draw.tin;
	#var screen[rows] %0;
	#var screen[cols] %1;
	#var screen[height] %2;
	#var screen[width] %3
}

/* Initialize Screen Variables */
#screen get rows screen[rows]
#screen get cols screen[cols]
#NOP #screen get height screen[height]
#NOP #screen get width screen[width]

/* Mouse input/buffer/chat scrolling */
#var screen[scroll] page
#event {SCROLLED MOUSE WHEEL UP} {
	#if {%0 > $screen[rows] - $split[bottom]} {
		#cursor {HISTORY PREV};
	};
	#if {%0 > $split[top] && %0 < $screen[rows] - $split[bottom]} {
		#if {"$screen[scroll]" == "page"} {
			#buffer up $screen[rows]
		}; #elseif {"$screen[scroll]" == "line"} {
			#buffer up 1
		}
	};
	#if {%0 < $split[top]} {
		chatup
	}
}

#event {SCROLLED MOUSE WHEEL DOWN} {
	#if {%0 > $screen[rows] - $split[bottom]} {
		#cursor {HISTORY NEXT};
	};
	#if {%0 > $split[top] && %0 < $screen[rows] - $split[bottom]} {
		#if {"$screen[scroll]" == "page"} {
			#buffer down $screen[rows]
		}; #elseif {"$screen[scroll]" == "line"} {
			#buffer down 1
		}
	};
	#if {%0 < $split[top]} {
		chatdown
	}
}

/* Double Click to toggle scrolling method per pane */
#event {DOUBLE-CLICKED MOUSE BUTTON ONE} {
	#if {%0 > $screen[rows] - $split[bottom]} {
		#return
	};
	#if {%0 > $split[top] && %0 < $screen[rows] - $split[bottom]} {
		#if {"$screen[scroll]" == "page"} {
			#var screen[scroll] line
		} {
			#var screen[scroll] page
		}
	};
	#if {%0 < $split[top]} {
		#if {"$chat[scroll]" == "page"} {
			#var chat[scroll] line
		} {
			#var chat[scroll] page
		}
	}
}


/* Screen Split resize on Mouse drag */
#EVENT {PRESSED MOUSE BUTTON ONE} {
	#if {%0 == $split[top]} {
		#if {%3 == -$split[right]} {
			#var DRAG 2
		} {
			#var DRAG 1
		}
	}
}

#EVENT {RELEASED MOUSE BUTTON ONE} {
	#variable DRAG 0;
	#NOP #screen raise SCREEN RESIZE
}

#EVENT {MOVED MOUSE BUTTON ONE} {
	#switch {$DRAG} {
		#case {0} {
			#return
		};
		#case {1} {
			#screen clear square 1 1 $split[top] -1;
			#variable split[top] %0
		};
		#case {2} {
			#screen clear square 1 1 $split[top] -1;
			#screen clear square $split[top] -$split[right] -$split[bottom]-2 -1;
			#var split[top] %0;
			#var canvas[width] -%3
		}
	};
}

/* Map mouse clicks*/

#NOP Display room info on single-click
#event {MAP SHORT-CLICKED MOUSE BUTTON ONE} {
	#map at %0 #map info
}

#NOP Map goto room on double-click
#event {MAP DOUBLE-CLICKED MOUSE BUTTON ONE} {
	#map goto %0
}

#NOP Map run on right-click
#event {MAP SHORT-CLICKED MOUSE BUTTON THREE} {
	#map run %0
}

#class {events} {close}

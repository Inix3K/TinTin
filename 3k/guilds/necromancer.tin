#class {necromancer} {open}

#var guild necromancer

/* HP Bar */
#act {{HP\[(\d+)/(\d+)\] SP\[(\d+)\(?(\d+)?\)?/(\d+)\] NP\[(\d+)/(\d+)\|(\d+)c\] E\[([\w\~\-\d\%\s\,\']+):?(\w+)?\]}} {
	#var {my[hp][current]}		{%2};
	#var {my[hp][max]}		{%3};
	#var {my[sp][current]}		{%4};
	#var {my[sp][fuse][current]}	{%5};
	#var {my[sp][max]}		{%6};
	#var {my[gp1][current]}		{%7};
	#var {my[gp1][max]}		{%8};
	#var {corpses[inventory]}	{%9};
	#var {ehealth}			{%11};
}

#act {{Status\[w([\d]*)\%\|p(\w+)\|v(\w+)\|r(\d+)\%\] Cr\[(\d+)\%\]}} {
	#var {my[worth]}	{%2};
	#var {my[prot]}	{%3};
	#var {my[gxp]}	{%6};
	#if {$my[worth] < 125} {
		con
	};
	#if {"$my[prot]" == "OFF"} {
		prot
	};
}

#alias {update_status} {
	#NOP Use SPFUSE;
	#if {"$my[sp][fuse][current]" == ""} {#var my[sp][fuse][current] 0};
	#if {$my[sp][max] - $my[sp][current] > $my[sp][fuse][current] &&
	     $my[sp][fuse][current] > 0} {
		#delay {spfuse} {#send !spfuse $my[sp][fuse][current]} {0.5};
	};
	#NOP Use dream;
	#math {my[hp][open]} {$my[hp][max] - $my[hp][current]};
	#if {$my[hp][open] < 0} {#var my[hp][open] 0};
	#math {my[sp][open]} {$my[sp][max] - $my[sp][current] + $my[sp][fuse][max] - $my[sp][fuse][current]};
	#if {$my[sp][open] < 0} {#var my[sp][open] 0};
	#if {($my[hp][open] + $my[sp][open]) > 500} {
		#delay {dream} {#send !dream zzzz} {0.5}
	};
	#if {$my[gp1][current] < $my[gp1][max]*.8 && $corpses[inventory] > 0} {
		#if {$my[sp][fuse][current] > 0} {
			spfuse $my[sp][fuse][current]
		};
		#delay {absorb} {#send !absorb} {0.5}
	};
	#if {$my[hp][current] < 400 && $corpses[inventory] > 0} {
		ab2
	};
	#if {"$cooler" == "on" || $corpses[cooler] > 0} {
		#format {sb1_cooler} {%+2s} {$corpses[cooler]};
		#var {sb1_cooler} { CL:<130>$sb1_cooler<088>}
	} {
		#var {sb1_cooler} {}
	};
	#if {"$freezer" == "on" || $corpses[freezer] > 0} {
		#format {sb2_freezer} {%+2s} {$corpses[freezer]};
		#var {sb2_freezer} { FZ:<130>$sb2_freezer<088>}
	} {
		#var {sb2_freezer} {}
	};
	#var {my[hp][graph]}		{@graph{$my[hp][current];$my[hp][max]}};
	#var {my[sp][graph]}		{@graph{$my[sp][current];$my[sp][max]}};
	#var {my[gp1][graph]}		{@graph{$my[gp1][current];$my[gp1][max]}};
	#var {enemy[graph]}		{@graph{$enemy[hp];100}};
	#format {hp}			{%+4s} {@colscale{$my[hp][current];$my[hp][max]}};
	#format {hp_max}		{%+4s} {<128>$my[hp][max]<088>};
	#format {sp}			{%+4s} {@colscale{$my[sp][current];$my[sp][max]}};
	#format {sp_inf}		{%+3s} {<168>$my[sp][fuse][current]<088>};
	#format {sp_max}		{%+4s} {<128>$my[sp][max]<088>};
	#format {np}			{%+5s} {@colscale{$my[gp1][current];$my[gp1][max]}};
	#format {np_max}		{%+5s} {<128>$my[gp1][max]<088>};
	#format {ehealth}		{%+8s} {@colscale{$enemy[hp];100}\%};
	#format {corpses[inventory]}	{%+2s} {$corpses[inventory]};
	#format {corpses[coffin]}	{%+2s} {$corpses[coffin]};
	#format {corpses[freezer]}	{%+2s} {$corpses[freezer]};
	#showme {<088>HP:$hp/$hp_max SP:$sp($sp_inf)/$sp_max NP:$np/$np_max  E:$ehealth    IC:<138>${corpses[inventory]}<088>${sb1_cooler}} {-4};
	#showme {H:$my[hp][graph] SP:$my[sp][graph]     NP:$my[gp1][graph]   E:$enemy[graph] CF:<138>$corpses[coffin]<088>${sb2_freezer}} {-3};
	#showme {$mip[gline][1]  $mip[gline][2]} {-6}
}

#alias {corpsetrig} {
	pp
}

#act {dealt the killing blow to} {corpsetrig}

/* Preserve all */
#var preserving 0

#alias {mpp} {
	#var preserving 1;
	#var preserve_np 0;
	#var preserve_done 0;
	qtrance preserve remains>20
}

#act {You only have %d/%d necromantic points for that.} {
	#if {$preserving} {
		#var preserve_np 1
	}
}
#act {That corpse is already preserved master.} {
	#if {$preserving} {
		#var preserve_done 1
	}
}
#act {There is no corpse in this room master.} {
	#if {$preserving} {
		#var preserve_done 1
	}
}

#act {You recover your senses} {
	#if {$preserving} {
		#if {$preserve_np} {
			absorb;
			mpp
		}; #elseif {$preserve_done} {
			#var preserving 0
		}; #else {
			mpp
		}
	}
}

#class {necromancer} {close}

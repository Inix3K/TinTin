#class {botload} {kill}
#class {botload} {open}

/* Global Bot Variable Initialization */
#var mobname {}

/* Load specific stepper file */
#alias {- %1} {
	#var no_home 0;
	#var no_loop 0;
	#var hardmode 0;
	#var vacuum 1;
	#if {"%1" == "miner"} {
		#read .tt/3k/craft/miner.tin
	} {
		#var bots[stepper] %1;
		#read .tt/3k/bots/%1.tin;
		#read .tt/3k/bots/bot_main.tin
	}
}

/* Resume a stepper after a disconnect DisConnectResume */
#alias {.dcr} {
	#map run $bots[vnum];
        - $bots[stepper];
        #delay {path_position} {#path goto $bots[position]} {0.5};
        #delay {stepper_start} {..} {2}
}

/* Party Variable*/
#var party {}

#NOP Add party member to list
#NOP NOTE: With disguised players, you should add an appropriote short desc: i.e., "pa Black cat"
#alias pa {#list party add %0}

#NOP Remove party member from list
#NOP Note: For multi-named (disguised characters manually added), use braces: i.e., "pr {Black cat}"
#alias pr {
        #list party find %0 party_index;
        #list party delete $party_index
}

#NOP Auto build party variable based off of party member list
#action {^Name         Location                                  Creator$} {
	#action {^%w %+ (%w%s)} {
		#var {party_member} {%%1};
		#var party_found 0;
		#list party find $party_member party_found;
		#if {$party_found == 0} {
			#list party add $party_member
		}
	} {1};
	#delay {party_capture} {
		#unaction {^%w %+ (%w%s)};
	} {0.5}
}

#NOP Auto add players on rally

#NOP Auto remove players on pboot or pquit


/* Low priority aliases to mute output from mud */
#alias {.bot%*} {#showme !bot is not active} {9}
#alias {.stop%*} {
	#if {"$miner" == "on"} {
		#class miner kill
	} {
		#showme !Bot is not active
	}
} {9}
#alias {.resume%*} {#showme !Bot is not active} {9}
#alias {killbot%*} {#showme !Bot is not active} {9}

#class {botload} {close}
